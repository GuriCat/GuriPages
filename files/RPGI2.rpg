     H            Y
      *ファイル定義
     FSYOHINL UF  E           K        DISK         KINFDS DBFDS A
     F                                              KINFSR #DBEX
     FDSPF2   CF  E                    WORKSTN
      *メッセージ格納用コンパイル時配列
     E                    MSG     1  13 78
      *ファイル情報データ構造
     IDBFDS       DS
     I                                       11  15 DBFSTS
     I                                     *OPCODE  DBFOPC
      *********************
      *メインルーチン
      *********************
      *
     C           *LOVAL    SETLLSYOHINR                76
     C      76             EXSR #DBEX
     C                     EXSR #INIT
      *
     C                     DO   *HIVAL
     C                     EXFMTDSPF2R
     C                     SETOF                     5051
     C                     SELEC
      * F3=終了
     C           *IN03     WHEQ *ON
     C                     LEAVE
      *更新/ F7=前/ F8=次
     C           *IN07     WHEQ *ON
     C           *IN08     OREQ *ON
     C                     MOVEA'100'     *IN,41
     C   60                EXSR #UPDAT
     C  N50                EXSR #MOVE
      * F9=挿入
     C           *IN09     WHEQ *ON
     C                     MOVEA'010'     *IN,41
     C                     EXSR #ADD
     C                     EXSR #INIT
      * F11=初期画面
     C           *IN11     WHEQ *ON
     C                     MOVEA'100'     *IN,41
     C                     EXSR #INIT
      * F23=削除
     C           *IN23     WHEQ *ON
     C                     MOVEA'001'     *IN,41
     C                     EXSR #DEL
      * ENTER(実行)
     C                     OTHER
     C           *IN60     IFEQ *ON
     C           *IN30     ANDEQ*ON
     C                     EXSR #UPDAT
     C                     ELSE
     C                     EXSR #CHAIN
     C                     ENDIF
     C                     ENDSL
     C                     ENDDO
      *
     C                     SETON                     LR
     C                     RETRN
      *********************
      *サブルーチン
      *********************
      *###初期画面表示
     C           #INIT     BEGSR
      *初期画面→明細非表示
     C                     CLEARSYOHINR
     C                     SETOF                     60
     C                     MOVEA'100'     *IN,41
     C                     ENDSR
      *###レコード更新
     C           #UPDAT    BEGSR
      *データが変更されていれば更新
     C           *IN30     IFEQ *ON
     C                     UPDATSYOHINR                70
      *更新エラー→MSG(3)表示、明細表示（現レコード）
     C           *IN70     IFEQ *ON
     C                     SETON                     50
     C                     MOVELMSG,3     MSGDTA
     C                     EXSR #DBSTS
     C                     SETON                     60
      *更新正常終了→MSG(8)表示、更新数+1
     C                     ELSE
     C                     SETON                     51
     C                     MOVELMSG,8     MSGDTA
     C                     ADD  1         UPDNUM
     C                     ENDIF
     C                     ENDIF
     C                     ENDSR
      *###次／前ページ
     C           #MOVE     BEGSR
      *初期画面の場合は部分キーで位置づけ
     C           *IN60     IFEQ *OFF
     C           PRPRCD    ANDNE*BLANKS
     C           PRPRCD    SETLLSYOHINR                76
     C      76             EXSR #DBEX
     C                     ENDIF
      *前／後のレコード読み込み
     C      07             READPSYOHINR                7671
     C      08             READ SYOHINR                7671
     C      76             EXSR #DBEX
      *最初・最後のレコードを超えた→MSG(2)表示
     C           *IN71     IFEQ *ON
     C      07   *HIVAL    SETLLSYOHINR                76
     C      08   *LOVAL    SETLLSYOHINR                76
     C      76             EXSR #DBEX
     C                     SETON                     51
     C                     MOVELMSG,2     MSGDTA
     C                     EXSR #INIT
      *エラーなし→MSG非表示、明細表示（移動先レコード）
     C                     ELSE
     C                     SETON                     60
     C                     ENDIF
     C                     ENDSR
      *###レコード追加
     C           #ADD      BEGSR
      *空レコードの表示
     C                     CLEARSYOHINR
     C                     SETON                     60
     C                     DO   *HIVAL
     C                     EXFMTDSPF2R
     C                     MOVEA'00'      *IN,50
      *追加取消→MSG(12)表示、初期画面
     C           *IN03     IFEQ *ON
     C                     MOVEA'01'      *IN,50
     C                     MOVELMSG,12    MSGDTA
     C                     LEAVE
     C                     ENDIF
      *キー未入力エラー→MSG(7)表示、明細表示
     C           PRPRCD    IFEQ *BLANKS
     C                     MOVEA'10'      *IN,50
     C                     MOVELMSG,7     MSGDTA
     C                     ENDIF
      *レコードの書出し
     C           *IN50     IFEQ *OFF
     C                     WRITESYOHINR                72
      *追加エラー→MSG(4)表示、明細表示（現レコード）
     C           *IN72     IFEQ *ON
     C                     MOVEA'10'      *IN,50
     C                     MOVELMSG,4     MSGDTA
     C                     EXSR #DBSTS
     C                     ELSE
      *追加OK→MSG(10)表示、追加モードの終了
     C                     MOVEA'01'      *IN,50
     C                     MOVELMSG,10    MSGDTA
     C                     ADD  1         ADDNUM
      *追加レコードに位置づけ
     C           PRPRCD    SETLLSYOHINR                76
     C      76             EXSR #DBEX
     C                     LEAVE
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
     C                     ENDSR
      *###レコード削除
     C           #DEL      BEGSR
      *レコードの削除→MSG(6)表示
     C                     SETON                     51
     C                     MOVELMSG,6     MSGDTA
     C                     SETON                     6061
      *削除画面→F23で削除
     C                     EXFMTDSPF2R
     C           *IN23     IFEQ *ON
     C                     DELETSYOHINR                73
      *削除エラー→MSG(5)表示、明細表示（現レコード）
     C           *IN73     IFEQ *ON
     C                     SETON                     50
     C                     MOVELMSG,5     MSGDTA
     C                     EXSR #DBSTS
     C                     SETON                     60
     C                     SETOF                       61
      *削除完了→MSG(9)表示、初期画面
     C                     ELSE
     C                     MOVEA'01'      *IN,50
     C                     MOVELMSG,9     MSGDTA
     C                     ADD  1         DLTNUM
     C                     SETOF                       61
      *削除レコードに位置づけ
     C           PRPRCD    SETLLSYOHINR                76
     C      76             EXSR #DBEX
     C                     EXSR #INIT
     C                     ENDIF
      *削除取消→MSG(11)表示、初期画面
     C                     ELSE
     C                     MOVEA'01'      *IN,50
     C                     MOVELMSG,11    MSGDTA
     C                     SETOF                       61
     C                     EXSR #INIT
     C                     ENDIF
     C                     ENDSR
      *###レコードキー読み込み
     C           #CHAIN    BEGSR
     C           PRPRCD    CHAINSYOHINR              7476
     C      76             EXSR #DBEX
      *キーなしエラー→MSG(1)表示、明細非表示
     C           *IN74     IFEQ *ON
     C                     MOVEA'10'      *IN,50
     C                     MOVELMSG,1     MSGDTA
     C                     SETOF                     60
     C                     ELSE
     C                     SETON                     60
     C                     ENDIF
     C                     ENDSR
      *### DBエラーメッセージ
     C           #DBSTS    BEGSR
     C                     CAT  DBFOPC:1  MSGDTA
     C                     CAT  DBFSTS:1  MSGDTA
     C                     ENDSR
      *### DB例外処理(INFSR)
     C           #DBEX     BEGSR
     C                     MOVELMSG,13    MSGDTA
     C                     EXSR #DBSTS
     C                     MOVELMSGDTA    DSPMSG 50
     C           DSPMSG    DSPLY          DUMMY   1
     C                     ENDSR'*CANCL'
**
商品コードが見つからない。
最初または最後のレコードを超えた。
レコードの更新でエラー。
レコードの追加でエラー。
レコードの削除でエラー。
レコードを削除するにはF23。F3または実行キーで取消。
製品コードが未入力。
レコードが更新された。
レコードが削除された。
レコードが追加された。
レコードの削除が取り消された。
レコードの追加が取り消された。
ＤＢ入出力で回復不能エラー。